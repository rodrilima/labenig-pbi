table medidas_financeiro
	lineageTag: 931ad143-9708-4495-ba4d-484963756635

	/// Soma do saldo de todos os t√≠tulos em aberto (saldo > 0)
	measure 'CR Total a Receber' = ```
			
			VAR DataFinal =
			    MAX ( calendario[Date] )
			RETURN   
			CALCULATE(
			    SUM(protheus_contasReceber[saldo]),
			    protheus_contasReceber[saldo] > 0, 
			    REMOVEFILTERS ( calendario ),
			    calendario[Date] <= DataFinal
			)
			```
		formatString: R$ #,##0.00
		displayFolder: Recebimentos
		lineageTag: 10de0ace-94cb-4c7b-8a31-d7bcec1448b5

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Valor total recebido (t√≠tulos baixados), incluindo juros e multa
	measure 'CR Recebido no Per√≠odo' =
			
			CALCULATE(
			    SUMX(
			        protheus_contasReceber,
			        protheus_contasReceber[valor_titulo] +
			        protheus_contasReceber[valor_juros] +
			        protheus_contasReceber[valor_multa]
			    ),
			    NOT(ISBLANK(protheus_contasReceber[data_baixa]))
			)
		formatString: R$ #,##0.00
		displayFolder: Recebimentos
		lineageTag: 26c03b56-2cf3-42a1-8ed8-4e7f42e0bac6

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Saldo de t√≠tulos vencidos e ainda n√£o pagos (data_vencimento < hoje)
	measure 'CR Valor Inadimplente' =
			
			CALCULATE(
			    SUM(protheus_contasReceber[saldo]),
			    protheus_contasReceber[saldo] > 0,
			    protheus_contasReceber[data_vencimento] < TODAY()
			)
		formatString: R$ #,##0.00
		displayFolder: Inadimpl√™ncia
		lineageTag: ed756031-7a46-4f13-8cee-82c1c4744557

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Total de saldo em aberto no per√≠odo selecionado
	measure 'CR Total' = SUM(protheus_contasReceber[valor_titulo])
		formatString: R$ #,##0.00
		displayFolder: Recebimentos
		lineageTag: c13ef7e8-fac8-47fe-a6a5-401e2756e597

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Percentual de inadimpl√™ncia: valor vencido / total em aberto
	measure 'CR Inadimpl√™ncia %' =
			
			DIVIDE(
			    [CR Valor Inadimplente],
			    [CR Total]
			)
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: Inadimpl√™ncia
		lineageTag: a4eeb8c2-e6db-4b2f-96fc-5241adb8a566

	/// Prazo M√©dio de Recebimento em dias, ponderado pelo valor do t√≠tulo. Exclui outliers acima de 180 dias ou antecipa√ß√µes acima de 60 dias.
	measure 'CR PMR (dias)' =
			
			DIVIDE(
			    SUMX(
			        FILTER(
			            protheus_contasReceber,
			            NOT(ISBLANK(protheus_contasReceber[data_baixa])) &&
			            DATEDIFF(
			                protheus_contasReceber[data_vencimento],
			                protheus_contasReceber[data_baixa],
			                DAY
			            ) >= -60 &&
			            DATEDIFF(
			                protheus_contasReceber[data_vencimento],
			                protheus_contasReceber[data_baixa],
			                DAY
			            ) <= 180
			        ),
			        DATEDIFF(
			            protheus_contasReceber[data_vencimento],
			            protheus_contasReceber[data_baixa],
			            DAY
			        ) * protheus_contasReceber[valor_titulo]
			    ),
			    CALCULATE(
			        SUM(protheus_contasReceber[valor_titulo]),
			        NOT(ISBLANK(protheus_contasReceber[data_baixa]))
			    )
			)
		formatString: 0.0 " dias"
		displayFolder: PMR
		lineageTag: 61b03866-4a60-4303-b412-a54f43b63a1b

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// PMR do m√™s anterior ao per√≠odo selecionado, para c√°lculo de varia√ß√£o
	measure 'CR PMR Per√≠odo Anterior' =
			
			CALCULATE(
			    [CR PMR (dias)],
			    DATEADD(calendario[Date], -1, MONTH)
			)
		formatString: 0.0 " dias"
		displayFolder: PMR
		lineageTag: 5936107d-ab0b-41c0-b444-8f22ac477652

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Varia√ß√£o em dias do PMR vs per√≠odo anterior. Positivo = piorou; negativo = melhorou.
	measure 'CR Varia√ß√£o PMR' =
			
			[CR PMR (dias)] - [CR PMR Per√≠odo Anterior]
		formatString: 0.0 " dias"
		displayFolder: PMR
		lineageTag: dc03d6da-4489-4930-ad85-f164872ac32f

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Valor base para an√°lise de aging (curva de vencimentos). Usar com faixas de dias para estratificar o portf√≥lio.
	measure 'CR Valor Aging' =
			
			VAR DataFinal =
			    MAX ( calendario[Date] )
			
			RETURN
			CALCULATE(
			    SUM ( protheus_contasReceber[saldo] ),
			    protheus_contasReceber[saldo] > 0,
			    REMOVEFILTERS ( calendario ),
			    calendario[Date] <= DataFinal
			)
		formatString: R$ #,##0.00
		displayFolder: Aging
		lineageTag: 2bc60ee4-fc28-4225-aecb-57218f467b29

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Valor inadimplente calculado at√© a data m√°xima do per√≠odo selecionado no calend√°rio
	measure 'CR Valor Inadimplente (Posi√ß√£o)' =
			
			CALCULATE(
			    SUM(protheus_contasReceber[saldo]),
			    FILTER(
			        ALL(protheus_contasReceber),
			        protheus_contasReceber[data_emissao] <= MAX(calendario[Date]) &&
			        (
			            ISBLANK(protheus_contasReceber[data_baixa]) ||
			            protheus_contasReceber[data_baixa] > MAX(calendario[Date])
			        ) &&
			        protheus_contasReceber[data_vencimento] < MAX(calendario[Date])
			    )
			)
		formatString: R$ #,##0.00
		displayFolder: Inadimpl√™ncia
		lineageTag: aaa2e6d8-f943-4301-8924-dd7904b156b8

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Total em aberto calculado at√© a data m√°xima do per√≠odo selecionado no calend√°rio
	measure 'CR Total em Aberto (Posi√ß√£o)' =
			
			CALCULATE(
			    SUM(protheus_contasReceber[saldo]),
			    FILTER(
			        ALL(protheus_contasReceber),
			        protheus_contasReceber[data_emissao] <= MAX(calendario[Date]) &&
			        (
			            ISBLANK(protheus_contasReceber[data_baixa]) ||
			            protheus_contasReceber[data_baixa] > MAX(calendario[Date])
			        )
			    )
			)
		formatString: R$ #,##0.00
		displayFolder: Inadimpl√™ncia
		lineageTag: b30ec5c9-792c-4dfd-9891-2b4d20e61943

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Percentual de inadimpl√™ncia calculado sobre a posi√ß√£o do per√≠odo selecionado no calend√°rio
	measure 'CR Inadimpl√™ncia % (Posi√ß√£o)' =
			
			VAR total_mes =
			    [CR Total em Aberto (Posi√ß√£o)]
			RETURN
			IF(
			    total_mes > 0,
			    DIVIDE(
			        [CR Valor Inadimplente (Posi√ß√£o)],
			        total_mes
			    ),
			    BLANK()
			)
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: Inadimpl√™ncia
		lineageTag: 92b7a087-e8f7-4090-ae7c-4f9a520faba4

	/// Valor em aberto por cliente, usado na tabela de detalhamento de inadimpl√™ncia
	measure 'CR Valor em Aberto por Cliente' =
			
			CALCULATE(
			    SUM(protheus_contasReceber[saldo]),
			    protheus_contasReceber[saldo] > 0
			)
		formatString: R$ #,##0.00
		displayFolder: Status Cliente
		lineageTag: fcb96510-1d6d-44f7-a096-d61ccb4a8562

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Quantidade de dias em atraso do t√≠tulo mais antigo do cliente no contexto atual
	measure 'CR Dias em Atraso' =
			
			VAR dias =
			    DATEDIFF(
			        MIN(protheus_contasReceber[data_vencimento]),
			        TODAY(),
			        DAY
			    )
			RETURN
			IF(dias > 0, dias, 0)
		formatString: 0
		displayFolder: Status Cliente
		lineageTag: cf00e4e9-ef1a-4e05-82b7-a8de313b0ae9

	/// Classifica√ß√£o do cliente por situa√ß√£o de atraso: Em Dia / Atraso Leve (at√© 30d) / Moderado (at√© 90d) / Grave (90d+)
	measure 'CR Status do Cliente' =
			
			SWITCH(
			    TRUE(),
			    [CR Dias em Atraso] = 0, "Em Dia",
			    [CR Dias em Atraso] <= 30, "Atraso Leve",
			    [CR Dias em Atraso] <= 90, "Atraso Moderado",
			    "Atraso Grave"
			)
		displayFolder: Status Cliente
		lineageTag: f54af439-07bc-4bb2-94ed-ff45a10b2ab5

	/// Saldo total de obriga√ß√µes em aberto (Contas a Pagar)
	measure 'CP Total a Pagar' =
			CALCULATE(
			    SUM(protheus_contasPagar[saldo]),
			    protheus_contasPagar[saldo] > 0
			)
		formatString: R$ #,##0.00
		displayFolder: CP Pagamentos
		lineageTag: 03f4100c-86ab-4379-9576-9401408385a7

	/// Valor total pago (t√≠tulos baixados), incluindo juros e multa
	measure 'CP Pagamentos Realizados no Per√≠odo' =
			CALCULATE(
			    SUMX(
			        protheus_contasPagar,
			        protheus_contasPagar[valor_titulo] +
			        protheus_contasPagar[valor_juros] +
			        protheus_contasPagar[valor_multa]
			    ),
			    NOT(ISBLANK(protheus_contasPagar[data_baixa]))
			)
		formatString: R$ #,##0.00
		displayFolder: CP Pagamentos
		lineageTag: eb786ca9-3f74-4f99-92d4-cd244398a1de

	/// Saldo de t√≠tulos vencidos e ainda n√£o pagos
	measure 'CP Valor em Atraso' =
			CALCULATE(
			    SUM(protheus_contasPagar[saldo]),
			    protheus_contasPagar[saldo] > 0,
			    protheus_contasPagar[data_vencimento] < TODAY()
			)
		formatString: R$ #,##0.00
		displayFolder: CP Atraso
		lineageTag: 88b93010-f077-48ea-b31b-06b876c8f26f

	/// Quantidade de t√≠tulos vencidos e n√£o pagos
	measure 'CP Qtd T√≠tulos em Atraso' =
			CALCULATE(
			    COUNTROWS(protheus_contasPagar),
			    protheus_contasPagar[saldo] > 0,
			    protheus_contasPagar[data_vencimento] < TODAY()
			)
		formatString: 0
		displayFolder: CP Atraso
		lineageTag: 320501e6-04d4-43ff-a83e-a2f6d02c4888

	/// Prazo M√©dio de Pagamento em dias, ponderado pelo valor do t√≠tulo
	measure 'CP PMP (dias)' =
			DIVIDE(
			    SUMX(
			        FILTER(
			            protheus_contasPagar,
			            NOT(ISBLANK(protheus_contasPagar[data_baixa])) &&
			            DATEDIFF(
			                protheus_contasPagar[data_emissao],
			                protheus_contasPagar[data_baixa],
			                DAY
			            ) >= 0 &&
			            DATEDIFF(
			                protheus_contasPagar[data_emissao],
			                protheus_contasPagar[data_baixa],
			                DAY
			            ) <= 365
			        ),
			        DATEDIFF(
			            protheus_contasPagar[data_emissao],
			            protheus_contasPagar[data_baixa],
			            DAY
			        ) * protheus_contasPagar[valor_titulo]
			    ),
			    CALCULATE(
			        SUM(protheus_contasPagar[valor_titulo]),
			        NOT(ISBLANK(protheus_contasPagar[data_baixa]))
			    )
			)
		formatString: 0.0 " dias"
		displayFolder: CP PMP
		lineageTag: e4e242ca-6701-49f2-be78-0a00b65b53fc

	/// Valor total programado a pagar (t√≠tulos em aberto, pelo valor original)
	measure 'CP Total Programado' =
			CALCULATE(
			    SUM(protheus_contasPagar[valor_titulo]),
			    protheus_contasPagar[saldo] > 0
			)
		formatString: R$ #,##0.00
		displayFolder: CP Pagamentos
		lineageTag: 1f4e8a10-f2bc-48a6-9000-3db158f7f225

	/// Saldo em aberto para an√°lise de aging (usar com faixa_aging)
	measure 'CP Valor Aging' = ```
			
			VAR DataFinal =
			    MAX ( calendario[Date] )
			
			RETURN
			CALCULATE(
			    SUM(protheus_contasPagar[saldo]),
			    protheus_contasPagar[saldo] > 0,    
			    REMOVEFILTERS(calendario),
			    calendario[Date] <= DataFinal
			)
			```
		formatString: R$ #,##0.00
		displayFolder: CP Aging
		lineageTag: 95966676-729d-427a-91ab-b24d889256d8

	measure 'Total Entrada' =
			
			CALCULATE(
			    SUM(protheus_movimentosBancarios[valor]),
			    protheus_movimentosBancarios[receb_pagto] = "R"
			)
			+
			CALCULATE(
			    SUM(projecao_caixa[valor_titulo]),
			    projecao_caixa[PR] = "R"
			)
		lineageTag: 640c1a1e-f1fa-4158-9481-b23ca010254c

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Total Sa√≠da' =
			
			CALCULATE(
			    SUM(protheus_movimentosBancarios[valor]),
			    protheus_movimentosBancarios[receb_pagto] = "P"
			)
			+
			CALCULATE(
			    SUM(projecao_caixa[valor_titulo]),
			    projecao_caixa[PR] = "P"
			)
		lineageTag: 10128ec6-e5fa-469b-8cdb-8d5305a4b6ca

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Saldo Projetado (depois)' =
			
			VAR SaldoInicial = SUM ( protheus_contasBancarias[saldo_atual] )
			VAR DataEixo     = MAX ( calendario[Date] )
			VAR DataInicio   = MINX ( ALLSELECTED ( calendario[Date] ), calendario[Date] )
			
			VAR TemMovNoDia =
			    CALCULATE (
			        COUNTROWS ( projecao_caixa ),
			        USERELATIONSHIP ( calendario[Date], projecao_caixa[data_vencimento_ajustada] ),
			        KEEPFILTERS ( calendario[Date] = DataEixo )
			    )
			
			VAR AcumAteDia =
			    CALCULATE (
			        SUM ( projecao_caixa[valorpn] ),
			        USERELATIONSHIP ( calendario[Date], projecao_caixa[data_vencimento_ajustada] ),
			        FILTER (
			            ALL ( calendario[Date] ),
			            calendario[Date] >= DataInicio
			                && calendario[Date] <= DataEixo
			        )
			    )
			
			RETURN
			    IF ( TemMovNoDia = 0, BLANK(), SaldoInicial + AcumAteDia )
		lineageTag: 41637e26-7271-49b6-abb8-861781c86cb6

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Saldo Realizado (antes)' =
			
			VAR SaldoFuturo =
			    SUM ( protheus_contasBancarias[saldo_atual] )
			VAR DataEixo =
			    MAX ( calendario[Date] )
			VAR TemMovNoDia =
			    CALCULATE (
			        COUNTROWS ( protheus_movimentosBancarios ),
			        KEEPFILTERS ( protheus_movimentosBancarios[data_movimento] = DataEixo )
			    )
			VAR SomaAPartirDoDia =
			    CALCULATE (
			        SUM ( protheus_movimentosBancarios[valorpn] ),
			        FILTER ( ALL ( calendario[Date] ), calendario[Date] >= DataEixo )
			    )
			RETURN
			    IF ( TemMovNoDia = 0, BLANK(), SaldoFuturo - SomaAPartirDoDia )
		lineageTag: 6169de71-d0f7-455b-bf17-4dab9d9c47a2

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Saldo Inicial (caixa)' =
			
			VAR SaldoBase =
			    SUM ( protheus_contasBancarias[saldo_atual] )
			
			VAR DataNoFiltro =
			    MIN ( calendario[Date] )
			
			VAR InicioMes =
			    DATE ( YEAR ( DataNoFiltro ), MONTH ( DataNoFiltro ), 1 )
			
			VAR FimMes =
			    EOMONTH ( InicioMes, 0 )
			
			-- üîé Existe qualquer dado no m√™s? (realizado OU proje√ß√£o)
			VAR TemDadosNoMes =
			    CALCULATE (
			        COUNTROWS ( protheus_movimentosBancarios ),
			        FILTER (
			            ALL ( calendario[Date] ),
			            calendario[Date] >= InicioMes
			                && calendario[Date] <= FimMes
			        )
			    )
			    +
			    CALCULATE (
			        COUNTROWS ( projecao_caixa ),
			        FILTER (
			            ALL ( calendario[Date] ),
			            calendario[Date] >= InicioMes
			                && calendario[Date] <= FimMes
			        )
			    )
			
			-- üîé Existe realizado no m√™s?
			VAR TemMovRealizado =
			    CALCULATE (
			        COUNTROWS ( protheus_movimentosBancarios ),
			        FILTER (
			            ALL ( calendario[Date] ),
			            calendario[Date] >= InicioMes
			                && calendario[Date] <= FimMes
			        )
			    )
			
			VAR SaldoInicialRealizado =
			    SaldoBase
			        - CALCULATE (
			            SUM ( protheus_movimentosBancarios[valorpn] ),
			            FILTER ( ALL ( calendario[Date] ), calendario[Date] >= InicioMes )
			        )
			
			VAR SaldoInicialProjetado =
			    SaldoBase
			        + CALCULATE (
			            SUM ( projecao_caixa[valorpn] ),
			            FILTER ( ALL ( calendario[Date] ), calendario[Date] < InicioMes )
			        )
			RETURN
			    IF (
			        ISBLANK ( DataNoFiltro ) || TemDadosNoMes = 0,
			        BLANK (),
			        IF ( TemMovRealizado > 0, SaldoInicialRealizado, SaldoInicialProjetado )
			    )
		lineageTag: 6af20a64-f7a3-4ffe-ba72-61b1ce621444

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Saldo Projetado (antes)' =
			
			VAR SaldoInicial = SUM ( protheus_contasBancarias[saldo_atual] )
			VAR DataEixo     = MAX ( calendario[Date] )
			VAR DataInicio   = MINX ( ALLSELECTED ( calendario[Date] ), calendario[Date] )
			VAR TemMovNoDia =
			    CALCULATE (
			        COUNTROWS ( projecao_caixa ),
			        USERELATIONSHIP ( calendario[Date], projecao_caixa[data_vencimento_ajustada] ),
			        KEEPFILTERS ( projecao_caixa[data_vencimento_real] = DataEixo )
			    )
			VAR AcumAteDiaAnterior =
			    CALCULATE (
			        SUM ( projecao_caixa[valorpn] ),
			        USERELATIONSHIP ( calendario[Date], projecao_caixa[data_vencimento_ajustada] ),
			        FILTER (
			            ALL ( calendario[Date] ),
			            calendario[Date] >= DataInicio
			                && calendario[Date] < DataEixo
			        )
			    )
			RETURN
			    IF ( TemMovNoDia = 0, BLANK(), SaldoInicial + AcumAteDiaAnterior )
		lineageTag: 5ea174a2-f54c-4e1c-90de-d774b7cc1437

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Saldo Realizado (depois)' =
			
			VAR SaldoFuturo =
			    SUM ( protheus_contasBancarias[saldo_atual] )
			VAR DataEixo =
			    MAX ( calendario[Date] )
			VAR TemMovNoDia =
			    CALCULATE (
			        COUNTROWS ( protheus_movimentosBancarios ),
			        KEEPFILTERS ( protheus_movimentosBancarios[data_movimento] = DataEixo )
			    )
			VAR SomaDepoisDoDia =
			    CALCULATE (
			        SUM ( protheus_movimentosBancarios[valorpn] ),
			        FILTER ( ALL ( calendario[Date] ), calendario[Date] > DataEixo )
			    )
			RETURN
			    IF ( TemMovNoDia = 0, BLANK(), SaldoFuturo - SomaDepoisDoDia )
		lineageTag: d760297c-dd0e-442a-a6f6-d7b809b74e5c

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Movimenta√ß√£o Projetada' =
			
			CALCULATE( SUM(projecao_caixa[valorpn]), USERELATIONSHIP(calendario[Date], projecao_caixa[data_vencimento_ajustada]) )
		formatString: "R$"\ #,0.###############;-"R$"\ #,0.###############;"R$"\ #,0.###############
		lineageTag: e7c7e485-ff21-4d3a-84bb-0929a7f3c90e

		annotation PBI_FormatHint = {"currencyCulture":"pt-BR"}

	measure 'CP Dias em Atraso' =
			
			VAR dataMin =
			    CALCULATE(
			        MIN(protheus_contasPagar[data_vencimento]),
			        protheus_contasPagar[status_pagamento] = "Atrasado"
			    )
			VAR dias =
			    IF(
			        NOT ISBLANK(dataMin),
			        DATEDIFF(dataMin, TODAY(), DAY)
			    )
			RETURN
			IF(dias > 0, dias, 0)
		formatString: 0
		displayFolder: Status Cliente
		lineageTag: 21a4fb22-be77-4c71-be1c-30b8d73b2aa9

	measure 'CP Status do Fornecedor' =
			
			SWITCH(
			    TRUE(),
			    [CP Dias em Atraso] = 0, "Em Dia",
			    [CP Dias em Atraso] <= 30, "Atraso Leve",
			    [CP Dias em Atraso] <= 90, "Atraso Moderado",
			    "Atraso Grave"
			)
		displayFolder: Status Cliente
		lineageTag: b20f7d9c-95cd-42c4-aee0-d3bf7ceceb3a

	measure 'CP Valor em Aberto por Fornecedor' =
			
			CALCULATE(
			    SUM(protheus_contasPagar[saldo]),
			    protheus_contasPagar[saldo] > 0
			)
		displayFolder: Status Cliente
		lineageTag: 7410346b-ad8f-4c46-a199-42dd9a5b571a

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	partition medidas_financeiro = m
		mode: import
		source =
				let
				    Fonte = Table.FromRows(Json.Document(Binary.Decompress(Binary.FromText("i44FAA==", BinaryEncoding.Base64), Compression.Deflate)), let _t = ((type nullable text) meta [Serialized.Text = true]) in type table [#"Coluna 1" = _t]),
				    #"Tipo Alterado" = Table.TransformColumnTypes(Fonte,{{"Coluna 1", type text}}),
				    #"Colunas Removidas" = Table.RemoveColumns(#"Tipo Alterado",{"Coluna 1"})
				in
				    #"Colunas Removidas"

	annotation PBI_NavigationStepName = Navega√ß√£o

	annotation PBI_ResultType = Table

